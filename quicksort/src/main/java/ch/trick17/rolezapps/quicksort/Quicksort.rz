package ch.trick17.rolezapps.quicksort

import rolez.util.Random
import rolez.util.StopWatch
import rolez.util.Scanner

/* To inspect JITed code, run with
 * -XX:+UnlockDiagnosticVMOptions -XX:+LogCompilation -XX:+TraceClassLoading -XX:+PrintAssembly */

class Quicksort {
    
    val maxLevel: int
    
    new {
        this.maxLevel = 3; // 8 tasks
    }
    
    new(maxLevel: int) {
        this.maxLevel = maxLevel;
    }
    
    def pure shuffledInts(n: int, random: readwrite Random): readwrite Array[int] {
        val array = new Array[int](n);
        for(var i = 0; i < n; i++)
            array.set(i, i);
        
        for(var i = n - 1; i > 0; i -= 1) {
            val index: int = random.nextInt(i + 1);
            
            val t = array.get(index);
            array.set(index, array.get(i));
            array.set(i, t);
        }
        return array;
    }
    
    def pure sort(a: readwrite Array[int]): {}
}

class QuicksortRolez extends Quicksort {
    
    new{}
    
    new(maxLevel: int) {
        super(maxLevel);
    }
    
    override pure sort(a: readwrite Array[int]): {
        this.doSort(a, 0, a.length, 0);
    }
    
    task pure doSort(s: readwrite Slice[int], begin: int, end: int, level: int): {
        val pivot = this.pivot(s, begin, end);
        var  left = begin;
        var right = end - 1;
        while(left <= right) {
            while(s.get( left) < pivot)  left++;
            while(s.get(right) > pivot) right--;
            if(left <= right) {
                val temp = s.get(left);
                s.set(left, s.get(right));
                s.set(right, temp);
                left++;
                right--;
            }
        }
        
        val sortLeft  = begin < right;
        val sortRight = left  < end - 1;
        
        // TODO: Switch to optimized version when maxLevel is reached
        if(level < this.maxLevel) {
            // create slices and do in parallel if both need doing
            if(sortLeft && sortRight) {
            	parallel
            		this.doSort(s.slice(begin, right + 1), begin, right + 1, level + 1);
            	and
            		this.doSort(s.slice( left,       end),  left,       end, level + 1);
            } else if(sortLeft) {
            	this.doSort(s.slice(begin, right + 1), begin, right + 1, level + 1);
            } else if(sortRight) {
            	this.doSort(s.slice( left,       end),  left,       end, level + 1);
            }
        }
        else {
            if(sortLeft)  this.doSort(s, begin, right + 1, level + 1);
            if(sortRight) this.doSort(s,  left,       end, level + 1);
        }
    }
    
    def pure pivot(s: readonly Slice[int], begin: int, end: int): int {
        // TODO: Use "ninther" for large enough slices
        val l = s.get(begin);
        val m = s.get(begin + (end - begin) / 2);
        val r = s.get(end - 1);
        if(l < m) {
            if(m < r) return m;
            else if(l < r) return l;
            else return r;
        }
        else { // m <= l
            if(l < r) return l;
            else if(m < r) return r;
            else return m;
        }
    }
    
    task pure main(args: readonly Array[String]): {
        val n = Int.parseInt(args.get(0));
        val iterations = 20;
        val watch = new StopWatch;
        
        val random = new Random;
        val arrays = new Array[readwrite Array[int]](iterations);
        
        for(var i = 0; i < iterations; i++)
            arrays.set(i, this.shuffledInts(n, random));
        
        System.out.println("Press Enter to start");
        new Scanner(System.in).nextLine;
        for(var i = 0; i < iterations; i++) {
            watch.go;
            this.sort(arrays.get(i));
            System.out.println(watch.get);
        }
    }
}