package ch.trick17.rolezapps.histogram

import rolez.io.PrintStream
import ch.trick17.rolezapps.histogram.util.ImageReader

class Histogram {
    
    val image: readonly Array[readwrite Array[int]]
    
    val rHist: readwrite Array[int] = new Array[int](256)
    val gHist: readwrite Array[int] = new Array[int](256)
    val bHist: readwrite Array[int] = new Array[int](256)
    
    new(image: readonly Array[readwrite Array[int]]) {
        this.image = image;
    }
    
    def readwrite compute(numTasks: int): {
        // partition
        val ranges = the ContiguousPartitioner.partition(this.image.range, numTasks);
        
        // map
        val tasks = new Array[pure Task[readwrite HistPart]](numTasks);
        for(var i = 0; i < numTasks - 1; i++)
            tasks.set(i, this start computePart(ranges.get(i + 1)));
        val part0 = this.computePart(ranges.get(0));
        
        // reduce
        this.merge(part0);
        for(var i = 0; i < numTasks - 1; i++)
            this.merge(tasks.get(i).get);
    }
    
    task readonly computePart(range: pure SliceRange): readwrite HistPart {
        val r: readwrite VectorBuilder[int] = new VectorBuilder[int](256);
        val g: readwrite VectorBuilder[int] = new VectorBuilder[int](256);
        val b: readwrite VectorBuilder[int] = new VectorBuilder[int](256);
        
        for(var y = range.begin; y < range.end; y+= range.step) {
            val row = this.image.get(y);
            for(var x = 0; x < row.length; x++) {
                val color = new Color(row.get(x));
                r.set(color.r, r.get(color.r) + 1);
                g.set(color.g, g.get(color.g) + 1);
                b.set(color.b, b.get(color.b) + 1);
            }
        }
        return new HistPart(r.build, g.build, b.build);
    }
    
    def readwrite merge(histPart: readonly HistPart): {
        for(var c = 0; c < 256; c++) {
            this.rHist.set(c, this.rHist.get(c) + histPart.r.get(c));
            this.gHist.set(c, this.gHist.get(c) + histPart.g.get(c));
            this.bHist.set(c, this.bHist.get(c) + histPart.b.get(c));
        }
    }
    
    def readonly print(width: int, binSize: int, out: readonly PrintStream): {
        var max = 0;
        for(var c = 0; c < 256; c++) {
           max = the Math.max(max, this.rHist.get(c));
           max = the Math.max(max, this.gHist.get(c));
           max = the Math.max(max, this.bHist.get(c));
        }
        
        val scaleWidth = width as double / max;
        
        out.println("Red:");
        this.printChannel(this.rHist, scaleWidth, binSize, out);
        the System.out.println("Green:");
        this.printChannel(this.gHist, scaleWidth, binSize, out);
        the System.out.println("Blue:");
        this.printChannel(this.bHist, scaleWidth, binSize, out);
    }
    
    def pure printChannel(hist: readonly Array[int], scaleWidth: double, binSize: int,
            out: readonly PrintStream): {
        var c = 0;
        while(c < 256) {
            var total = 0.0;
            for(var j = 0; j < binSize; j++) {
                total += hist.get(c);
                c++;
            }
            val barSize = (total / binSize * scaleWidth) as int;
            this.printBar(barSize, out);
            out.println;
        }
    }
    
    def pure printBar(size: int, out: readonly PrintStream): {
        for(var i = 0; i < size; i++)
            out.print("â–„");
    }
}

class HistPart {
    val r: pure Vector[int]
    val g: pure Vector[int]
    val b: pure Vector[int]
    
    new(r: pure Vector[int], g: pure Vector[int], b: pure Vector[int]) {
        this.r = r;
        this.g = g;
        this.b = b;
    }
}

object HistogramApp {
    
    task pure main: {
        val image = the ImageReader.read("a.jpg");
        val histogram = new Histogram(image);
        
        histogram.compute(8);
        
        histogram.print(80, 8, the System.out);
    }
}