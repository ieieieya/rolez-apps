package ch.trick17.rolezapps.montecarlo

import rolez.io.File
import rolez.util.ArrayList
import rolez.util.Scanner

class MonteCarloApp {
    
    val pathStartValue: double = 100.0
    
    val steps: int
    val runs: int
    val numTasks: int

    new(steps: int, runs: int, numTasks: int) {
        this.steps = steps;
        this.runs = runs;
        this.numTasks = numTasks;
    }
    
    def readwrite run: {}
    def readonly avgExpectedReturnRate: double { return 0.0; }
}

class MonteCarloAppRolez extends MonteCarloApp {
    
    val returns: pure Returns
    
    val seeds: readwrite Array[long]
    val results: readwrite ArrayList[double] = new ArrayList[double]
    
    new(steps: int, runs: int, numTasks: int, ratesFile: pure String) {
        super(steps, runs, numTasks);
        
        this.returns = new Returns(the RatePathReader.readRatesFile(ratesFile));
        
        this.seeds = new Array[long](runs);
        for(var i = 0; i < runs; i++)
            this.seeds.set(i, i * 11 as long);
    }
    
    override readwrite run: {
        val partitions = this.seeds.partition(the ContiguousPartitioner, this.numTasks);
        
        val tasks = new ArrayList[pure Task[readwrite ArrayList[double]]];
        for(var i = 0; i < this.numTasks; i++)
            tasks.add(this start simulate(partitions.get(i)));
        
        for(var i = 0; i < this.numTasks; i++)
            this.results.addAll(tasks.get(i).get);
    }
    
    task pure simulate(seeds: readonly Slice[long]): readwrite ArrayList[double] {
        val results = new ArrayList[double];
        for(var i = seeds.range.begin; i < seeds.range.end; i += seeds.range.step) {
            val mcPath = new MonteCarloPath(this.returns, this.steps);
            mcPath.computeFluctuations(seeds.get(i));
            mcPath.computePathValues(this.pathStartValue);
            results.add(new Returns(mcPath).expectedReturnRate);
        }
        return results;
    }
    
    override readonly avgExpectedReturnRate: double {
        var result = 0.0;
        for(var i = 0; i < this.runs; i++)
            result += this.results.get(i);
        result /= this.runs;
        return result;
    }
}

object MonteCarloAppRolezRunner {
    
    val file: pure String = "Data/hitData"
    val steps: int = 1000
    
    task pure main: {
        val app = new MonteCarloAppRolez(this.steps, 100000, 8, this.file);
        
        the System.out.println("Press Enter to start");
        new Scanner(the System.in).nextLine;
        
        app.run;
        the System.out.println(app.avgExpectedReturnRate);
    }
}