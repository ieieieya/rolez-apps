package ch.trick17.rolezapps.montecarlo

import rolez.io.File
import rolez.util.ArrayList
import rolez.util.Scanner

class MonteCarloApp {
    
    val pathStartValue: double = 100.0
    
    val runs: int
    val numTasks: int
    
    val returns: pure Returns
    val steps: int
    
    val seeds: readwrite Array[int]
    val results: readwrite ArrayList[double] = new ArrayList[double]
    
    new(ratesFile: pure String, steps: int, runs: int, numTasks: int) {
        this.steps = steps;
        this.runs = runs;
        this.numTasks = numTasks;
        
        this.returns = new Returns(the RatePathReader.readRatesFile(ratesFile));
        
        this.seeds = new Array[int](runs);
        for(var i = 0; i < runs; i += 1)
            this.seeds.set(i, i * 11);
    }
    
    def readwrite run: {
        val partitions = this.seeds.partition(the ContiguousPartitioner, this.numTasks);
        
        val tasks = new ArrayList[pure Task[readwrite ArrayList[double]]];
        for(var i = 1; i < this.numTasks; i += 1)
            tasks.add(this start simulate(partitions.get(i)));
        
        this.results.addAll(this.simulate(partitions.get(0)));
        
        for(var i = 0; i < tasks.size; i += 1)
            this.results.addAll(tasks.get(i).get);
    }
    
    task pure simulate(seeds: readonly Slice[int]): readwrite ArrayList[double] {
        val results = new ArrayList[double];
        for(var i = seeds.range.begin; i < seeds.range.end; i += seeds.range.step) {
            val mcPath = new MonteCarloPath(this.returns, this.steps);
            mcPath.computeFluctuations(seeds.get(i));
            mcPath.computePathValues(this.pathStartValue);
            results.add(new Returns(mcPath).expectedReturnRate);
        }
        return results;
    }
    
    def readonly avgExpectedReturnRate: double {
        var result = 0.0;
        for(var i = 0; i < this.runs; i += 1)
            result += this.results.get(i);
        result /= this.runs;
        return result;
    }
}

object MonteCarloAppRunner {
    
    val file: pure String = "Data/hitData"
    val steps: int = 1000
    
    task pure main: {
        val app = new MonteCarloApp(this.file, this.steps, 100000, 8);
        
        the System.out.println("Press Enter to start");
        new Scanner(the System.in).nextLine;
        
        app.run;
        the System.out.println(app.avgExpectedReturnRate);
    }
}