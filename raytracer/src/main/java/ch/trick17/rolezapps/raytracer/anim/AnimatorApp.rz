package ch.trick17.rolezapps.raytracer.anim

import rolez.util.ArrayList
import rolez.util.Random
import rolez.util.StopWatch
import ch.trick17.rolezapps.raytracer.*
import ch.trick17.rolezapps.raytracer.util.VideoWriter

object AnimatorApp {
    
    task pure main(args: readonly Array[pure String]): {
        val raytracer = new Raytracer;
        raytracer.numTasks = 8;
        raytracer.oversample = 2;
        
        val scene = new AnimatedScene(30.0);
        this.buildScene(scene, new Random);
        val width = 800;
        val height = (width / scene.view.aspect) as int;
        val writer = new VideoWriter("movie.mp4", width, height, 25, 12);
        
        val animator = new Animator(raytracer, scene, writer);
        animator.printProgress = true;
        animator.render;
    }
    
    def pure buildScene(scene: readwrite AnimatedScene, random: readwrite Random): {
        /* Parameters */
        val height  = 22.0;
        val side    = 40.0;
        val camDist = side/2 - 1;
        val ballsPerS = 4;
        
        val dimLength = 11.0;
        val darkLength = 1.0;
        
        /* View */
        val view = new View;
        view.from = new Vector3D(-camDist, 0.0, height/2 - 1);
        view.at   = new Vector3D(0.0, 0.0, 0.0);
        view.up   = new Vector3D(0.0, 0.0, 1.0);
        view.distance = 1.0;
        view.angle    = the Math.toRadians(35.0);
        view.aspect   = 16.0/9.0;
        scene.view = view;
        scene.animations.add(new CircularMovement(the WholeTime, view,
                new Vector3D(0.0, 0.0, height/2 - 1).minus(view.from), view.up, the Math.PI / 6));
        scene.animations.add(new LinearMovement(the WholeTime, view,
                new Vector3D(0.0, 0.0, -(height-2) / scene.length)));
        
        /* Materials */
        val red = new Material;
        red.color = new Color(0.9, 0.1, 0.1);
        red.kd    = 0.9;
        red.shine = 15.0;
        red.ks    = 0.5;

        val white = new Material;
        white.color = new Color(0.9, 0.9, 1.0);
        white.kd    = 1.2;
        white.shine = 100.0;
        white.ks    = 0.25;
        
        val glass = new Material;
        glass.color = the Colors.black;
        glass.kd    = 0.0;
        glass.shine = 15.0;
        glass.ks    = 0.2;
        glass.kt    = 1.0;
        glass.ior   = 1.05;
        
        val blue = new Material;
        blue.color = new Color(0.0, 0.0, 0.5);
        blue.kd    = 0.5;
        blue.shine = 10.0;
        blue.ks    = 0.2;
        
        val black = new Material;
        black.color = the Colors.black;
        black.kd    = 0.0;
        black.shine = 10.0;
        black.ks    = 0.2;
        
        /* Objects & lights */        
        val wall1 = new Plane(new Vector3D(-1.0,  0.0,  0.0),   side/2, white);
        val wall2 = new Plane(new Vector3D( 1.0,  0.0,  0.0),   side/2, white);
        val wall3 = new Plane(new Vector3D( 0.0, -1.0,  0.0),   side/2, white);
        val wall4 = new Plane(new Vector3D( 0.0,  1.0,  0.0),   side/2, white);
        val ceil  = new Plane(new Vector3D( 0.0,  0.0, -1.0), height/2, blue );
        val floor = new Plane(new Vector3D( 0.0,  0.0,  1.0), height/2, black);
        scene.objects.add(wall1);
        scene.objects.add(wall2);
        scene.objects.add(wall3);
        scene.objects.add(wall4);
        scene.objects.add(floor);
        scene.objects.add(ceil );
        
        val mainLight = new Light(new Vector3D(0.0, 0.0, 0.0), 5.0);
        scene.lights.add(mainLight);
        scene.animations.add(new BrightnessChange(new Duration(scene.length - dimLength - darkLength),
            mainLight, -mainLight.brightness / (0.5*dimLength)));
        
        scene.ambientLight = 0.2;
        scene.animations.add(new AmbientLightChange(new Duration(scene.length - 0.75*dimLength - darkLength),
            scene, -scene.ambientLight / (0.5*dimLength)));
        
        val lilLightsDimDuration = new Duration(scene.length - 0.5*dimLength - darkLength);
        for(var t = 0.0; t < scene.length; t += 1.0/ballsPerS) {
            val radius = the Math.abs(1.5 + 0.75 * random.nextGaussian);
            
            val distance = radius + 0.1 + (camDist - 2*radius - 0.2) * random.nextDouble;
            val angle = 2 * the Math.PI * random.nextDouble;
            val x = distance * the Math.cos(angle);
            val y = distance * the Math.sin(angle);
            
            val speed = the Math.abs(6 + 3 * random.nextGaussian);
            val duration = new Duration(t, t + (height + 2*radius) / speed);
            
            val ball = new Sphere(new Vector3D(x, y, -height/2-radius), radius, red);
            val light = new Light(new Vector3D(x, y + 1.2 * radius, - height/2-radius), 0.2);
            
            scene.animations.add(new BallAnimation(duration, ball, speed, scene));
            scene.animations.add(new LightAnimation(duration, light, ball.center, speed, the Math.PI, scene));
            scene.animations.add(new BrightnessChange(lilLightsDimDuration, light,
                -light.brightness / (0.5*dimLength)));
        }
    }
}

class BallAnimation extends LinearMovement {
    
    val scene: readwrite AnimatedScene
    
    new(duration: pure Duration, sphere: readwrite Sphere, speed: double, scene: readwrite AnimatedScene) {
        super(duration, sphere, new Vector3D(0.0, 0.0, 1.0).scale(speed));
        this.scene = scene;
    }
    
    override readwrite onBegin: {
        this.scene.objects.add(this.obj as readwrite Sphere);
    }

    override readwrite onFinish: {
        this.scene.objects.remove(this.obj);
    }
}

class LightAnimation extends AnimationGroup {
    
    val scene: readwrite AnimatedScene
    val light: readwrite Light
    
    new(duration: pure Duration, light: readwrite Light, center: pure Vector3D,
            riseSpeed: double, circularSpeed: double, scene: readwrite AnimatedScene) {
        super(the Helper.lightAnimations(duration, light, center, riseSpeed, circularSpeed));
        this.scene = scene;
        this.light = light;
    }
    
    override readwrite onFirstBegin: {
        this.scene.lights.add(this.light);
    }
    
    override readwrite onLastFinish: {
        this.scene.lights.remove(this.light);
    }
}

object Helper {
    def pure lightAnimations(duration: pure Duration, light: readwrite Light, center: pure Vector3D,
            riseSpeed: double, circularSpeed: double): readwrite Array[readwrite Animation] {
        val up = new Vector3D(0.0, 0.0, 1.0);
        val linear = new LinearMovement(duration, light, up.scale(riseSpeed));
        val circular = new CircularMovement(duration, light, center.minus(light.position), up, circularSpeed);
        
        val result = new Array[readwrite Animation](2);
        result.set(0, linear);
        result.set(1, circular);
        return result;
    }   
}